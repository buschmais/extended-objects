[[naming:Default]]
[role=group,includesConstraints="naming:*"]
== Naming Rules

This section describes naming rules that apply to the elements of the code base.

=== Java Classes

[[naming:TypeNameMustBeginWithGroupId]]
[source,cypher,role=constraint]
.All Java types must be located in packages that start with the groupId of the Maven project.
----
MATCH
  (project:Maven:Project)-[:CREATES]->(:Artifact)-[:CONTAINS]->(type:Type)
WHERE
  NOT type.fqn starts with project.groupId
RETURN
  project as Project, collect(type) as TypeWithWrongName
----

[[naming:TypeNameMustContainArtifactId]]
[source,cypher,role=constraint]
.All Java types must be located in package that contain the artifactId of the Maven Project.
----
MATCH
  (project:Maven:Project)-[:CREATES]->(:Artifact)-[:CONTAINS]->(type:Type)
WHERE
  NOT type.fqn contains project.artifactId
RETURN
  project as Project, collect(type) as TypeWithWrongName
----

[[naming:AbstractClassNamePrefix]]
[source,cypher,role=constraint]
.All abstract classes must have a name that starts with the prefix `Abstract`.
----
MATCH
  (c:Type:Class)
WHERE
  exists(c.abstract) and c.abstract=true
  and not c.name starts with "Abstract"
RETURN
  c as AbstractClass
----

[[naming:NonAbstractClassNamePrefix]]
[source,cypher,role=constraint,requiresConcepts="java:InnerType"]
.Non-abstract classes must have a name that does not start with the prefix `Abstract`.
----
MATCH
  (c:Type:Class)
WHERE
  not c:Inner
  and not (exists(c.abstract) and c.abstract=true)
  and c.name starts with "Abstract"
RETURN
  c as NonAbstractClass
----

